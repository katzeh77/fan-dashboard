#include <WiFi.h>
#include <PubSubClient.h>
#include <EEPROM.h>
#include <time.h>

// ===== WIFI =====
const char* ssid = "www.sexymom.biz";
const char* password = "Unyilunyil";
const char* mqtt_server = "broker.emqx.io";

WiFiClient espClient;
PubSubClient client(espClient);

// ===== LED =====
const int ledPins[8] = {5,18,19,21,22,23,25,26};

// ===== MODE =====
bool autoMode = false;
bool autoSaved = false;

int autoLevel[5] = {0,0,0,0,0};
int currentBrightness = 0;
int targetBrightness = 0;

unsigned long lastRamp = 0;
const int rampInterval = 200;

// ===== EEPROM =====
void loadSettings(){
  EEPROM.begin(256);

  EEPROM.get(0, autoMode);
  EEPROM.get(5, autoSaved);

  for(int i=0;i<5;i++){
    EEPROM.get(20 + i*4, autoLevel[i]);
    if(autoLevel[i]<0 || autoLevel[i]>255) autoLevel[i]=0;
  }
}

void saveSettings(){
  EEPROM.put(0, autoMode);
  EEPROM.put(5, autoSaved);

  for(int i=0;i<5;i++){
    EEPROM.put(20 + i*4, autoLevel[i]);
  }

  EEPROM.commit();
}

// ===== PWM =====
void setAllLED(int value){
  value = constrain(value,0,255);
  for(int i=0;i<8;i++){
    ledcAttachPin(ledPins[i], i);
    ledcSetup(i,5000,8);
    ledcWrite(i,value);
  }
}

// ===== AUTO TIME =====
int getAutoIndex(){
  struct tm timeinfo;
  if(!getLocalTime(&timeinfo)) return -1;

  int total = timeinfo.tm_hour*60 + timeinfo.tm_min;

  if(total>=390 && total<=450) return 0;
  if(total>=451 && total<=540) return 1;
  if(total>=541 && total<=780) return 2;
  if(total>=781 && total<=870) return 3;
  if(total>=871 && total<=960) return 4;

  return -1;
}

void updateAuto(){
  if(!autoMode) return;

  int idx = getAutoIndex();
  if(idx==-1) targetBrightness = 0;
  else targetBrightness = autoLevel[idx];

  if(millis()-lastRamp >= rampInterval){
    lastRamp = millis();

    if(currentBrightness < targetBrightness) currentBrightness++;
    else if(currentBrightness > targetBrightness) currentBrightness--;

    setAllLED(currentBrightness);
  }
}

// ===== MQTT CALLBACK =====
void callback(char* topic, byte* payload, unsigned int length){
  String t = String(topic);
  String msg="";
  for(int i=0;i<length;i++) msg+=(char)payload[i];

  // ===== SET AUTO LEVEL (belum disimpan) =====
  if(t.startsWith("auto/set")){
    int idx = t.substring(8).toInt()-1;
    if(idx>=0 && idx<5){
      autoLevel[idx] = constrain(msg.toInt(),0,255);
      autoSaved = false;
      client.publish("status/auto","LEVEL_UPDATED");
    }
  }

  // ===== SAVE AUTO =====
  if(t=="auto/save"){
    autoSaved = true;
    saveSettings();
    client.publish("status/auto","AUTO_SAVED");
  }

  // ===== MODE =====
  if(t=="mode"){
    if(msg=="auto"){
      if(autoSaved){
        autoMode = true;
        saveSettings();
        client.publish("status/mode","AUTO");
      } else {
        client.publish("status/mode","SAVE_DULU");
      }
    }

    if(msg=="manual"){
      autoMode = false;
      saveSettings();
      client.publish("status/mode","MANUAL");
    }
  }

  // ===== MANUAL LED =====
  if(!autoMode && t.startsWith("led/")){
    currentBrightness = constrain(msg.toInt(),0,255);
    setAllLED(currentBrightness);
  }
}

// ===== MQTT =====
void reconnect(){
  while(!client.connected()){
    if(client.connect("ESP32_LED_Controller")){
      client.subscribe("mode");
      client.subscribe("auto/set1");
      client.subscribe("auto/set2");
      client.subscribe("auto/set3");
      client.subscribe("auto/set4");
      client.subscribe("auto/set5");
      client.subscribe("auto/save");
      for(int i=1;i<=8;i++)
        client.subscribe(("led/"+String(i)).c_str());
    } else delay(3000);
  }
}

void setup(){
  Serial.begin(115200);

  WiFi.begin(ssid,password);
  while(WiFi.status()!=WL_CONNECTED) delay(500);

  client.setServer(mqtt_server,1883);
  client.setCallback(callback);

  configTime(7*3600,0,"pool.ntp.org");

  loadSettings();
}

void loop(){
  if(!client.connected()) reconnect();
  client.loop();
  updateAuto();
}
